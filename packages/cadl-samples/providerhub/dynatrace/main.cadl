import "@cadl-lang/rest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub-controller";
import "./models.cadl";

@armNamespace
@serviceTitle("Dynatrace.Observability")
@serviceVersion("2021-06-13-preview")
namespace Dynatrace.Observability;

using Azure.ARM;
using Azure.ResourceManager;
using Cadl.Http;

@doc("Dynatrace Monitor Resource")
@armResource({
  path: "monitors",
  collectionName: "Monitors",
  parameterType: MonitorNameParameter,
})
model MonitorResource is TrackedResource<MonitorProperties> {
  ...ManagedServiceIdentity; // change here to use the standard definition
}
model MonitorNameParameter {
  @doc("Monitor resource name")
  @path
  monitorName: string;
}

@doc("Tag rules for a monitor resource")
@armListBy(MonitorNameParameter, "List")
@armResource({
  path: "tagRules",
  parentResourceType: MonitorResource,
  collectionName: "TagRules",
  parameterType: RuleSetNameParameter,
})
model TagRule is ProxyResource<MonitoringTagRulesProperties> {}

model RuleSetNameParameter {
  @doc("Monitor RuleSet resource name")
  @path
  ruleSetName: string;
}

@armResourceOperations(MonitorResource)
namespace Monitors {
  @post
  @route("getAccountCredentials")
  @doc("Gets the user account credentials for a Monitor")
  op getAccountCredentials(
    ...ApiVersionParameter,
    ...SubscriptionIdParameter,
    ...ResourceGroupNameParameter,
    ...MonitorNameParameter
  ): AccountInfoSecure | ErrorResponse;

  @post
  @route("listMonitoredResources")
  @doc("List the resources currently being monitored by the Dynatrace monitor resource.")
  @pageable("nextLink")
  op listMonitoredResources(
    ...ApiVersionParameter,
    ...SubscriptionIdParameter,
    ...ResourceGroupNameParameter,
    ...MonitorNameParameter
  ): ArmResponse<MonitoredResourceListResponse> | ErrorResponse;

  @post
  @route("vmHostPayload")
  @doc("Returns the payload that needs to be passed in the request body for installing Dynatrace agent on a VM.")
  op getVMHostPayload(
    // needed to change the name here to make it a verb.
    ...ApiVersionParameter,
    ...SubscriptionIdParameter,
    ...ResourceGroupNameParameter,
    ...MonitorNameParameter
  ): ArmResponse<VMExtensionPayload> | ErrorResponse;

  @post
  @route("vmHostUpdate")
  @doc("Sending request to update the collection when Dynatrace agent has been installed on a VM for a given monitor.")
  @pageable("nextLink")
  op listVMHostUpdate(
    ...ApiVersionParameter,
    ...SubscriptionIdParameter,
    ...ResourceGroupNameParameter,
    ...MonitorNameParameter,
    ...VMHostUpdateParameter // change here
  ): ArmResponse<VMResourcesListResponse> | ErrorResponse;

  @post
  @route("listVMHosts")
  @doc("List the compute resources currently being monitored by the Dynatrace resource.")
  @pageable("nextLink")
  op listVMHosts(
    ...ApiVersionParameter,
    ...SubscriptionIdParameter,
    ...ResourceGroupNameParameter,
    ...MonitorNameParameter
  ): ArmResponse<VMResourcesListResponse> | ErrorResponse;

  @post
  @route("singleSignOnConfigurations")
  @doc("List the single sign-on configurations for a given monitor resource.")
  @pageable("nextLink")
  op listSingleSignOnConfigurations(
    // changed for compliance
    ...ApiVersionParameter,
    ...SubscriptionIdParameter,
    ...ResourceGroupNameParameter,
    ...MonitorNameParameter
  ): ArmResponse<VMResourcesListResponse> | ErrorResponse;
}

// note: I think you only need get.  It is odd to have PUT without DELETE, though.
@doc("Single sign-on configurations for a given monitor resource.")
@armListBy(MonitorNameParameter, "List")
@armResource({
  path: "singleSignOnConfigurations",
  parentResourceType: MonitorResource,
  collectionName: "SingleSignOn",
  parameterType: ConfigurationNameParameter,
  standardOperations: ["create", "read"], // check if this is correct. We only need these 2 operations
})
model DynatraceSingleSignOnResource is ProxyResource<DynatraceSingleSignOnProperties> {}

model ConfigurationNameParameter {
  @doc("Single Sign On Configuration Name")
  @path
  configurationName: string;
}
