import "@cadl-lang/rest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-providerhub-controller";
import "@azure-tools/cadl-azure-resource-manager";

@armNamespace
@serviceTitle("Azure PlayFab")
@serviceVersion("2021-06-15-preview")
namespace Microsoft.PlayFab;

using Azure.ARM;
using Azure.ResourceManager;
using Cadl.Http;

@doc("A string that represents a URI.")
model Uri is string {}

@doc("A reference to a resource of type title.")
@pattern("\\/subscriptions\\/[a-z0-9\\-]+\\/resourceGroups\\/[^\\/]+\\/providers\\/Microsoft\\.PlayFab\\/titles\\/[^\\/]+")
model TitleReference is string {}

@doc("A reference to a player database resource.")
@pattern("\\/subscriptions\\/[a-z0-9\\-]+\\/resourceGroups\\/[^\\/]+\\/providers\\/Microsoft\\.PlayFab\\/playerDatabases\\/[^\\/]+")
model PlayerDatabaseReference is string {}

@armResource({
  path: "playerDatabases",
  collectionName: "playerDatabases",
  parameterType: PlayerDatabaseNameParameter,
})
@doc("Details of the player database.")
model PlayerDatabase is TrackedResource<PlayerDatabaseProperties> {}

model PlayerDatabaseNameParameter {
  @doc("The name of the player database resource.")
  @path
  name: string;
}

@armResource({
  path: "titles",
  collectionName: "Titles",
  parameterType: TitleNameParameter,
})
@doc("Details of a title.")
model Title is TrackedResource<TitleProperties> {}

model TitleNameParameter {
  @doc("Name of the title resource.")
  @path
  name: string;
}

@doc("The properties of a player database.")
model PlayerDatabaseProperties {
  @doc("The user id for this player database")
  userId: string;

  @doc("The set of titles belonging to this player database.")
  titles?: TitleReference[];
  ...DefaultProvisioningStateProperty;
}

@doc("The properties of a title.")
model TitleProperties {
  @doc("The player database for this title")
  @visibility("create", "read")
  parentDatabase: PlayerDatabaseReference;
  ...DefaultProvisioningStateProperty;
}
