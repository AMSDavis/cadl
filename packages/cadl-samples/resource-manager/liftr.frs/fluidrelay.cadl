import "@cadl-lang/rest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

@armNamespace
@serviceTitle("Azure Fluid Relay")
@serviceVersion("2021-03-12-preview")
namespace Microsoft.FluidRelay;

using Cadl.Http;
using Cadl.Rest;
using Azure.ResourceManager;

@doc("A string that represents a URI.")
model Uri is string {}

@doc("The representation of a secret value, represented as a string on the wire")
@secret
model SecretKey is string {}

@doc("Details of the FluidRelay server.")
model FluidRelayServer is TrackedResource<FluidRelayServerProperties> {
  @path
  @key
  @doc("The resource name.")
  @segment("fluidRelayServers")
  name: string;
}

@armResourceOperations
interface FluidRelayServers mixes ResourceOperations<FluidRelayServer, FluidRelayServerProperties> {
  @doc("Regenerate the primary or secondary key for this server.")
  @post
  @armResourceAction(FluidRelayServer)
  regenerateKey(
    ...ResourceInstanceParameters<FluidRelayServer>,

    @doc("The details of the key generation request.")
    @body
    parameters: RegenerateKeyRequest
  ): ArmResponse<FluidRelayServerKeys> | ErrorResponse;

  @doc("Retrieve the keys for this server.")
  @post
  @armResourceAction(FluidRelayServer)
  getKeys(
    ...ResourceInstanceParameters<FluidRelayServer>
  ): ArmResponse<FluidRelayServerKeys> | ErrorResponse;
}

@doc("The properties of a Fluid Relay Service resource.")
model FluidRelayServerProperties {
  @doc("The Fluid tenantId for this server")
  @visibility("read")
  frsTenantId?: string;

  @doc("The Fluid service endpoints for this server.")
  @visibility("read")
  fluidRelayEndpoints?: FluidRelayEndpoints;

  @doc("Provisioning states for FluidRelay RP")
  provisioningState?: ProvisioningState;
}

@doc("Provisioning states for FluidRelay RP")
@knownValues(ProvisioningStateKV)
model ProvisioningState is string {}
enum ProvisioningStateKV {
  Succeeded,
  Failed,
  Canceled,
}

@doc("The Fluid Relay Service endpoints for this server.")
model FluidRelayEndpoints {
  @doc("The Fluid Relay Orderer Endpoints.")
  @visibility("read")
  ordererEndpoints?: Uri[];

  @doc("The Fluid Relay storage endpoints.")
  @visibility("read")
  storageEndpoints?: Uri[];
}

@doc("The set of available keys for this server.")
model FluidRelayServerKeys {
  @doc("The primary key for this server.")
  @visibility("read")
  key1?: SecretKey;

  @doc("The secondary key for this server.")
  @visibility("read")
  key2?: SecretKey;
}

@doc("The keys that are defined fro a FluidRelay Service.")
@knownValues(KeyNameKV)
model KeyName is string {}
enum KeyNameKV {
  key1,
  key2,
}

@doc("Specifies which key should be generated.")
model RegenerateKeyRequest {
  @doc("The key to regenerate.")
  keyName: KeyName;
}
