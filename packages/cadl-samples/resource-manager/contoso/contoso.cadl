import "@cadl-lang/rest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

@armNamespace
@serviceTitle("ContosoProviderHubClient")
@serviceVersion("2021-10-01-preview")
@doc("Contoso Resource Provider management API.")
namespace Microsoft.ContosoProviderHub;

using Cadl.Http;
using Cadl.Rest;
using Azure.ResourceManager;

@doc("A ContosoProviderHub resource")
model Employee is TrackedResource<EmployeeProperties> {
  @format("^[a-zA-Z0-9-]{3,24}$")
  @key("employeeName")
  @segment("employees")
  name: string;
}

model EmployeeProperties {
  age?: int32;
  city?: string;

  @visibility("read")
  @doc("The status of the last operation.")
  provisioningState?: ProvisioningState;
}

@armResourceOperations
interface Employees mixes ResourceOperations<Employee, EmployeeProperties> {}

@knownValues(ProvisioningStateKV)
model ProvisioningState is string {}
enum ProvisioningStateKV {
  Succeeded,
  Failed,
  Canceled,
  Provisioning,
  Updating,
  Deleting,
  Accepted,
}
